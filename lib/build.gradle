plugins {
    id 'java'
    id "com.diffplug.spotless" version "7.0.2"
    id("io.freefair.lombok") version "8.12.2.1"
    id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2025.+'
    id 'maven-publish'
 }

group = 'nukeminecart'
version = '1.2'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.withType(Javadoc) {
    options {
        docTitle = "Badger Log Javadoc"
    }
}

if (project.hasProperty('releaseMode')) {
    wpilibRepositories.addAllReleaseRepositories(project)
} else {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

dependencies {
    implementation 'edu.wpi.first.ntcore:ntcore-java:2025.+'
    
    implementation 'edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:2025.+'
    implementation 'edu.wpi.first.wpiunits:wpiunits-java:2025.+'
    implementation 'edu.wpi.first.wpimath:wpimath-java:2025.+'
    implementation 'edu.wpi.first.wpilibj:wpilibj-java:2025.+'
    implementation 'edu.wpi.first.wpiutil:wpiutil-java:2025.+'
    implementation 'edu.wpi.first.hal:hal-java:2025.+'

    implementation 'io.github.classgraph:classgraph:4.8.179'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = "com.github.nukeminecart"
            artifactId = "badger-log"
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

apply plugin: 'groovy'
spotless {
    enforceCheck false

    encoding 'UTF-8'
    lineEndings 'UNIX'

    java {
        licenseHeader '// Copyright (c) FIRST and other WPILib contributors.\n' +
                '// Open Source Software; you can modify and/or share it under the terms of\n' +
                '// the WPILib BSD license file in the root directory of this project.\n\n'
        toggleOffOn()
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        trimTrailingWhitespace()
        endWithNewline()
    }

    groovy {
        importOrder()
        excludeJava()
        greclipse()
    }

    groovyGradle {
        greclipse()
    }

    json {
        target '**/*.json'
        simple().indentWithSpaces(2)
    }

    format 'misc', {
        target '.gitignore'

        trimTrailingWhitespace()
        endWithNewline()
    }
}